
// Normal module configuration
//----------------------------------------------------------------------------------------------------------------------

apply plugin: 'java-library'

dependencies {

    // Dynamic Component Registration
    api 'com.google.inject:guice:4.2.0'
    api 'io.github.lukehutch:fast-classpath-scanner:2.18.2'

    // core utilities
    api "com.eden:Common:${JavaEden.Common}"
    api "com.eden:Clog4j:${JavaEden.Clog}"
    api "com.eden:Krow:${JavaEden.Krow}"
    api "org.projectlombok:lombok:1.16.20"
    api "com.squareup.okhttp3:okhttp:3.10.0"
    api "commons-io:commons-io:2.6"

    // Included parsers: JSON, YAML, TOML, CSV, Pebble, Markdown, Sass
    api 'org.json:json:20180130'
    api 'org.yaml:snakeyaml:1.21'
    api 'com.moandjiezana.toml:toml4j:0.7.2'
    api 'io.pebbletemplates:pebble:2.5.1'
    api 'io.bit3:jsass:5.7.0'
    api 'com.univocity:univocity-parsers:2.6.3'

    // Flexmark extensions
    api "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-aside:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-attributes:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-enumerated-reference:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-tables:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-gfm-tasklist:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-toc:$flexmarkVersion"

    // server
    api 'org.nanohttpd:nanohttpd:2.3.1'
    api 'org.nanohttpd:nanohttpd-websocket:2.3.1'

    // generate own documentation with Orchid
    orchidCompile mainProjects
}

// Make sure Core is compiled before plugins, but javadoc run after plugins
//----------------------------------------------------------------------------------------------------------------------

static boolean otherProjectsTask(Task task, String taskName) {
    return !task.path.startsWith(':OrchidCore') && task.path.endsWith(":${taskName}")
}

project.parent.subprojects.each { subproject ->
    project.tasks.javadoc.dependsOn subproject.tasks.matching { task -> otherProjectsTask(task, 'jar') }
    project.tasks.orchidRun.dependsOn subproject.tasks.matching { task -> otherProjectsTask(task, 'jar') }
    project.tasks.javadoc.shouldRunAfter subproject.tasks.matching { task -> otherProjectsTask(task, 'javadoc') }
}

// Generate a Class containing the current build version, so it can be injected into OrchidSite
//----------------------------------------------------------------------------------------------------------------------

task generateSources {
    def outputDir = file("$buildDir/generated-src")
    outputs.dir outputDir
    doFirst {
        def srcFile = new File(outputDir, "com/eden/orchid/OrchidVersion.java")
        srcFile.parentFile.mkdirs()
        srcFile.write("""
package com.eden.orchid;
public class OrchidVersion {
   public static String getVersion() { return "$project.version"; }
}
""")
    }
}
compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

// Add check to make sure every release version has a Changelog file
//----------------------------------------------------------------------------------------------------------------------

task checkForChangelogFile {
    doLast {
        def versionFilename = project.version.toString().replaceAll("\\.", "_") + ".md"
        def fullVersionFilename = "$projectDir/src/orchid/resources/changelog/$versionFilename"
        def versionFile = file(fullVersionFilename)

        if(project.hasProperty("release") && (project.property("release") != 'false') && !versionFile.exists()) {
            throw new FileNotFoundException("There is no changelog entry for this version, expected 'changelog/$versionFilename' to exist.")
        }
    }
}
check.dependsOn checkForChangelogFile